#!/bin/zsh
# infect - Single file user configuration file distribution system
# Written by Lowe Thiderman (lowe.thiderman@gmail.com)
# https://github.com/thiderman/infect
# Licensed under the BSD license.

# Preface {{{

SELF=$0:a

INFECT_HOST="https://etc.ninjaloot.se"
INFECT_DIR="$HOME/.local/share/infect"
INFECT_WEB_DIR="$HOME/var/srv/infect"
INFECT_REMOTE_WEB_DIR="nl:var/srv/infect"
INFECT_BACKUP="backup"
INFECT_RSYNC_EXCLUDE=".git* .hg* .svn* tmp/ $INFECT_BACKUP/"
INFECT_RC_FILE=$INFECT_DIR/infect.rc
INFECT_REPO_FILE=.local

mkdir -p $INFECT_DIR

# }}}
# Install {{{

function infect_install() {
  typeset -A apps
  typeset -A dest
  typeset -A force

  pushd $INFECT_DIR
  # local dir=${1:-$PWD}
  local dir=$PWD
  echo $dir:a > $INFECT_RC_FILE

  source $dir/*.src

  # Create symlinks for applications found on the system
  iecho "Installing symbolic links"
  for app in ${(ko)apps}; do
    if [[ -x $commands[$app] ]] || [[ -n "$force[$app]" ]]; then
      infect_symlink ${(z)${apps[$app]}}
    fi
  done

  # If post_infect_install is a function, run it.
  res=$(LC_ALL=C builtin type -w post_infect_install 2>/dev/null)
  case $res in
    *': function')  post_infect_install;;
  esac

  popd
}

function infect_symlink() {
  local target_dir=$(<$INFECT_RC_FILE)

  while [[ -n "$1" ]]; do
    local target=$1
    local backup=$INFECT_DIR/$INFECT_BACKUP

    # If there is a destination in the dest array, use it
    local dst=$dest[$target]
    if [[ -z "$dst" ]]; then
      # If not, put directly in $HOME with a dot prefix
      dst="$HOME/.$target:t"
    fi

    src=$target_dir/$target

    if [[ -L $dst ]]; then
      unlink $dst
    elif [[ -f $dst ]] || [[ -d $dst ]]; then
      mkdir -p $backup
      mv $dst $backup
      iecho "Backed up original %F{green}${dst}%f"
    fi

    mkdir -p $dst:h
    ln -s $src:a $dst:a
    iecho "%F{green}$1%F{white} -> %F{blue}${dst}"

    shift
  done
}

# }}}
# Deploy {{{

function infect_deploy() {
  local src=${1:-${INFECT_DIR}}
  local src=$src:A
  local dest=${2:-${INFECT_WEB_DIR}}
  local current="current.tgz"
  local file="$(date +"%Y.%m.%d").tgz"
  local stage="stage"

  iecho "Deploying to %F{blue}$dest%f..."
  if [[ ! -d $dest ]]; then
    iecho "Creating"
    # Create the directory or crash.
    if ! mkdir -p $dest; then
      iecho "Error: Could not create destination directory. Aborting deploy." red
      return 127
    fi
  fi

  cd $dest

  iecho "Installing current infect script as %F{magenta}index.html%f"
  cp $SELF ./index.html

  if [[ -f $file ]]; then
    iecho "Removing already deployed %F{yellow}$file%f"
    rm $file
  fi

  local exc=""
  for p in ${(z)INFECT_RSYNC_EXCLUDE}; do
    exc+="--exclude=$p "
  done

  iecho "Rsyncing..."
  mkdir -p ./$stage
  rsync -a $src/* $src/.* ./$stage/infect/ ${(z)exc} || return 1
  cd $stage

  # Oh, and replace all symbolic links with their actual targets.
  iecho "Hardening symbolic links..."
  setopt nullglob
  for link in **/*(@); do
    target=$(readlink_f $link)
    unlink $link
    cp -r $target $link
  done

  iecho "Compressing..."
  tar czf ./$file * || return 1
  mv $file ..

  cd ..
  rm -r $stage

  if [[ -L "$current" ]]; then
    iecho "Unlinking old current..."
    unlink $current
  fi

  iecho "Linking new current..."
  ln -s $file $current

  # iecho "Rsyncing to remote..."
  # rsync -a ./ $INFECT_REMOTE_WEB_DIR || return 1

  iecho "Deploy complete. %F{red}<3%f"
}

function readlink_f() {
  # This was originally taken from
  # http://stackoverflow.com/questions/1055671/how-can-i-get-the-behavior-of-gnus-readlink-f-on-a-mac
  # Idea is to have consistent 'readlink -f' behavior between Linux and OS X

  local target_file=$1

  cd $target_file:h
  target_file=$target_file:t

  # Iterate down a (possible) chain of symlinks
  while [ -L "$target_file" ]
  do
    target_file=$(readlink $target_file)
    cd $target_file:h
    target_file=$target_file:t
  done

  # Compute the canonicalized name by finding the physical path
  # for the directory we're in and appending the target file.
  phys_dir=$(pwd -P)
  echo $phys_dir/$target_file
}

# }}}
# Download / Update {{{

function infect_update() {
  local file="current.tgz"
  local target=$INFECT_DIR:t
  local repofile=$target/$INFECT_REPO_FILE

  if [[ -f $INFECT_RC_FILE ]]; then
    local dir=$(<$INFECT_RC_FILE)
    iecho "Going to %F{blue}$dir%f..."
    cd $dir/..
  else
    cd $INFECT_DIR/..
  fi

  # Download and extract the tar file
  iecho "Downloading..."
  curl -k --silent $INFECT_HOST/$file -O || return 1

  iecho "Extracting..."
  tar xzf $file || return 1
  rm $file

  if [[ -n "$1" ]] || [[ -f $repofile ]]; then
    if [[ -n "$1" ]]; then
      local repo=$1
      echo $repo > $repofile
    elif [[ -f $repofile ]]; then
      local repo=$(<$repofile)
    fi

    if [[ ! $repo =~ '/' ]]; then
      repo+="/conf-local"
    fi

    cd $target

    iecho "Downloading %F{cyan}$repo%f master zip file..."
    curl https://github.com/$repo/archive/master.zip -LO || return 1

    iecho "Unzipping..."
    unzip -qq master || return 1

    iecho "Mutating..."
    rsync -r --links ./conf-local-master/* . || return 1
    rm -r master.zip conf-local-master

    cd ..
  fi

  # XXX: Fix this shit
  # If using old structure, sync to it.
  if [[ -d "./config" ]]; then
    iecho "Syncing..."
    rsync -r ./etc/* ./config
    rm -r etc
  fi

  infect_install $target || return 1
  iecho "Infection complete. %F{red}<3%f" white 0
}

# }}}
# Uninstall {{{

function infect_uninstall() {
  typeset -A apps
  typeset -A dest
  typeset -A force

  local dir=${1:-$PWD}
  source $dir/*.src

  iecho "Uninstalling symbolic links"
  for app in ${(ko)apps}; do
    infect_unlink ${(z)${apps[$app]}}
  done
}

function infect_unlink() {
  while [[ -n "$1" ]]; do
    local target=$1
    local dst=$dest[$target]

    if [[ -n "$dst" ]]; then
      _unlink $dst || return 1
      # Non-home target. Make sure to remove empty parent directories!
      dst=$dst:h
      while rmdir $dst &> /dev/null; do
        iecho "Removing  %F{magenta}$dst%f"
        dst=$dst:h
      done
    else
      _unlink "$HOME/.$target:t" || return 1
    fi
    shift
  done
}

function _unlink() {
  if [[ -L $1 ]]; then
    iecho "Unlinking %F{cyan}$1%f"
    unlink $1 || return 1
  fi
}

# }}}
# Interface {{{

function iecho() {
  local color=${2:-white}
  if [[ -n "$INFECT_VERBOSE" ]] || [[ -n "$2" ]]; then
    print -P "%B%F{black}-> %F{${color}}${1}%f%b"
  fi
}

# }}}

# Runtime case selection {{{

arg=$1
if [[ -n "$1" ]]; then
  export INFECT_VERBOSE="<3"
  shift
else
  # If run without arguments, download latest and run that.
  iecho "Downloading latest version..." white 0
  curl -k --silent $INFECT_HOST | zsh -s - 'update' || exit 1
  exit 0
fi

case $arg in
  install)
    infect_install $*;;

  deploy)
    export INFECT_VERBOSE="<3"
    infect_deploy $*;;

  uninstall)
    infect_uninstall $*;;

  update)
    export INFECT_VERBOSE=""
    infect_update;;

  *)
    infect_update $arg;;
esac

# }}}

# vim: et sts=2 sw=2 fdm=marker fmr={{{,}}}
