#!/bin/zsh
# infect - Single file user configuration file distribution system
# Written by Lowe Thiderman (lowe.thiderman@gmail.com)
# https://github.com/thiderman/infect
# Licensed under the BSD license.

# Preface {{{

SELF=$0:a

INFECT_HOST="http://infect.ninjaloot.se"
INFECT_DIR="$HOME/etc"
INFECT_WEB_DIR="$HOME/var/srv/infect"
INFECT_BACKUP="backup"
INFECT_RSYNC_EXCLUDE=".git* .hg* .svn* tmp/ $INFECT_BACKUP/"
INFECT_RC_FILE=$HOME/.infect
INFECT_REPO_FILE=.local

# }}}
# Install {{{

function infect_install() {
  typeset -A apps
  typeset -A dest
  typeset -A force

  local dir=${1:-$PWD}
  echo $dir:a > $INFECT_RC_FILE
  source $dir/*.src

  # Create symlinks for applications found on the system
  iecho "Installing symbolic links"
  for app in ${(ko)apps}; do
    if [[ -x $commands[$app] ]] || [[ -n "$force[$app]" ]]; then
      infect_symlink ${(z)${apps[$app]}}
    fi
  done

  # If post_infect_install is a function, run it.
  res=$(LC_ALL=C builtin type -w post_infect_install 2>/dev/null)
  case $res in
    *': function')  post_infect_install;;
  esac
}

function infect_symlink() {
  local target_dir=$(<$INFECT_RC_FILE)

  while [[ -n "$1" ]]; do
    local target=$1
    local backup=$INFECT_DIR/$INFECT_BACKUP

    # If there is a destination in the dest array, use it
    local dst=$dest[$target]
    if [[ -z "$dst" ]]; then
      # If not, put directly in $HOME with a dot prefix
      dst="$HOME/.$target:t"
    fi

    src=$target_dir/$target

    if [[ -L $dst ]]; then
      unlink $dst
    elif [[ -f $dst ]] || [[ -d $dst ]]; then
      if [[ ! -d $backup ]]; then
        mkdir -p $backup
      fi
      mv $dst $backup
      iecho "Backed up original %F{green}${dst}%f"
    fi

    mkdir -p $dst:h &> /dev/null
    ln -s $src:a $dst:a
    iecho "%F{green}$1%F{white} -> %F{blue}${dst}"

    shift
  done
}

# }}}
# Deploy {{{

function infect_deploy() {
  local src=${1:-${INFECT_DIR}}
  local dest=${2:-${INFECT_WEB_DIR}}
  local current="current.tgz"
  local file="$(date +"%Y.%m.%d").tgz"

  iecho "Deploying to %F{blue}$dest%f..."
  if [[ ! -d $dest ]]; then
    # Create the directory or crash.
    if ! mkdir -p $dest; then
      iecho "Error: Could not create destination directory. Aborting deploy." red
      return 127
    fi
  fi

  cd $dest

  iecho "Installing current infect script as %F{magenta}index.html%f"
  cp $SELF ./index.html

  if [[ -f $file ]]; then
    iecho "Removing already deployed %F{yellow}$file%f"
    rm $file
  fi

  local exc=""
  for p in ${(z)INFECT_RSYNC_EXCLUDE}; do
    exc+="--exclude=$p "
  done

  iecho "Rsyncing..."
  rsync $src ./stage -a ${(z)exc} || return 1

  # This here with getting a sane structure in tarballs...
  cd stage
  iecho "Compressing..."
  tar czf $file * || return 1
  mv $file ..

  cd ..
  rm -r stage

  if [[ -L "$current" ]]; then
    iecho "Unlinking old current..."
    unlink $current
  fi

  iecho "Linking new current..."
  ln -s $file $current

  iecho "Deploy complete. %F{red}<3%f"
}

# }}}
# Download / Update {{{

function infect_update() {
  local file="current.tgz"
  local target=$INFECT_DIR:t
  local repofile=$target/$INFECT_REPO_FILE

  if [[ -f $INFECT_RC_FILE ]]; then
    local dir=$(<$INFECT_RC_FILE)
    iecho "Going to %F{blue}$dir%f..."
    cd $dir/..
  fi

  # Download and extract the tar file
  iecho "Downloading..."
  curl --silent $INFECT_HOST/$file -O || return 1

  iecho "Extracting..."
  tar xzf $file || return 1
  rm $file

  if [[ -n "$1" ]] || [[ -f $repofile ]]; then
    if [[ -n "$1" ]]; then
      local repo=$1
      echo $repo > $repofile
    elif [[ -f $repofile ]]; then
      local repo=$(<$repofile)
    fi

    if [[ ! $repo =~ '/' ]]; then
      repo+="/conf-local"
    fi

    cd $target

    iecho "Downloading %F{cyan}$repo%f master zip file..."
    curl --silent https://nodeload.github.com/$repo/zip/master -O || return 1

    iecho "Unzipping..."
    unzip -qq master || return 1

    iecho "Mutating..."
    rsync -r ./conf-local-master/* . || return 1
    rm -r master conf-local-master

    cd ..
  fi

  # XXX: Fix this shit
  # If using old structure, sync to it.
  if [[ -d "./config" ]]; then
    iecho "Syncing..."
    rsync -r ./etc/* ./config
    rm -r etc
  fi

  infect_install $target || return 1
  iecho "Infection complete. %F{red}<3%f"
}

# }}}
# Uninstall {{{

function infect_uninstall() {
  typeset -A apps
  typeset -A dest
  typeset -A force

  local dir=${1:-$PWD}
  source $dir/*.src

  iecho "Uninstalling symbolic links"
  for app in ${(ko)apps}; do
    infect_unlink ${(z)${apps[$app]}}
  done
}

function infect_unlink() {
  while [[ -n "$1" ]]; do
    local target=$1
    local dst=$dest[$target]

    if [[ -n "$dst" ]]; then
      _unlink $dst || return 1
      # Non-home target. Make sure to remove empty parent directories!
      dst=$dst:h
      while rmdir $dst &> /dev/null; do
        iecho "Removing  %F{magenta}$dst%f"
        dst=$dst:h
      done
    else
      _unlink "$HOME/.$target:t" || return 1
    fi
    shift
  done
}

function _unlink() {
  if [[ -L $1 ]]; then
    iecho "Unlinking %F{cyan}$1%f"
    unlink $1 || return 1
  fi
}

# }}}
# Interface {{{

function iecho() {
  local color=${2:-white}
  print -P "%B%F{black}-> %F{${color}}${1}%f%b"
}

# }}}

# Runtime case selection {{{

arg=$1
if [[ -n "$1" ]]; then
  shift
fi

case $arg in
  install)
    infect_install $*;;

  deploy)
    infect_deploy $*;;

  uninstall)
    infect_uninstall $*;;

  *)
    infect_update $arg;;
esac

# }}}

# vim: et sts=2 sw=2 fdm=marker fmr={{{,}}}
