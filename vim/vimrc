" Author: Lowe Thiderman <lowe.thiderman@gmail.com>
"
" Credit: Steve Losh <steve@stevelosh.com>
" Source: https://github.com/thiderman/dotfiles
" Source: http://bitbucket.org/sjl/dotfiles/src/tip/vim/

" Preamble {{{

set nocompatible
filetype off
filetype plugin indent on

" }}}
" Plugins {{{

let s:addons = []

" Colors {{{

let s:addons += [
  \ 'nanotech/jellybeans.vim',
\ ]

" }}}
" UI and navigation {{{

let s:addons += [
  \ 'itchyny/lightline.vim',
  \ 'scrooloose/nerdtree',
\ ]

" }}}
" Git {{{

let s:addons += [
\ ]

" }}}
" Python {{{

let s:addons += [
\ ]

" }}}
" Misc filetypes {{{

let s:addons += [
  \ 'evanmiller/nginx-vim-syntax',
  \ 'rodjek/vim-puppet',
  \ 'tpope/vim-markdown',
  \ 'elzr/vim-json',
\ ]

" }}}
" Completion and snippets {{{

let s:addons += [
\ ]

" }}}
" Editing and misc {{{

let s:addons += [
  \ 'Raimondi/delimitMate',
  \ 'tpope/vim-commentary',
  \ 'tpope/vim-endwise',
  \ 'tpope/vim-eunuch',
  \ 'tpope/vim-repeat',
  \ 'tpope/vim-surround',
  \ 'vim-scripts/argtextobj.vim',
\ ]

" }}}

" Initialization {{{

let s:addons = map(s:addons, '"github:".v:val')

let g:vim_addon_manager = {'shell_commands_run_method': 'system'}

" Use VAM. It's not my favorite and its documentation is pretty much
" unreadable, but it is declarative and this works flawlessly on a new clone
" without needing submodules or any such trickery.
fun! SetupVAM(addons)
  let c = get(g:, 'vim_addon_manager', {})
  let c.plugin_root_dir = expand('$HOME', 1) . '/.vim/vim-addons'
  let c.log_to_buf = 0
  let c.auto_install = 1
  let &rtp.=(empty(&rtp)?'':',').c.plugin_root_dir.'/vim-addon-manager'
  if !isdirectory(c.plugin_root_dir.'/vim-addon-manager/autoload')
    let cmd = '/usr/bin/git clone --depth=1 git://github.com/MarcWeber/vim-addon-manager '
    let cmd .= shellescape(c.plugin_root_dir.'/vim-addon-manager', 1)
    call system(cmd)
  endif
  call vam#ActivateAddons(a:addons, {'auto_install' : 1})
endfun

call SetupVAM(s:addons)

" The classic one!
runtime macros/matchit.vim
" }}}

" }}}
" Basic options {{{

" Basic settings {{{

set encoding=utf-8
set modelines=0
set autoindent
set showmode
set showcmd
set hidden
set novisualbell
set t_vb=
set updatetime=500
set ttyfast
set ruler
set backspace=indent,eol,start
set nonumber
if exists('+relativenumber')
  set norelativenumber
endif
set laststatus=2
set history=1000
if exists('+undofile')
  set undofile
  set undoreload=10000
endif
set list
set listchars=tab:▸\ ,trail:¬,extends:❯,precedes:❮
" set shell=/bin/bash\ --login
set lazyredraw
set matchtime=3
set showbreak=↪
set splitbelow
set splitright
set fillchars=diff:⣿,vert:│
set autowrite
set autoread
set shiftround
set title
set linebreak
set dictionary=/usr/share/dict/words
set spellfile=~/.vim/spell/custom-dictionary.utf-8.add
if exists('+colorcolumn')
  set colorcolumn=+1
endif

" Don't try to highlight lines longer than 800 characters.
set synmaxcol=800

" Time out on key codes but not mappings.
" Basically this makes terminal Vim work sanely.
set notimeout
set ttimeout
set ttimeoutlen=10

" Make Vim able to edit crontab files again.
set backupskip=/tmp/*,/private/tmp/*"

" Better Completion
set complete=.,w,b,u,t
set completeopt=longest,menuone,preview

" }}}
" Autosave {{{

" Save the file on delays or when exiting insert mode. Is automatically
" disabled for files not under git version control.
function! AutoSave()
  " We are not in git or the file is not modified. Do nothing.
  if exists('b:autosave') && b:autosave != 1 || &modified == 0
    return
  endif

  " We generally always want to run the BufWritePre. Only skip it if it is set
  " and set to zero.
  if !exists('b:autosave_bufwritepre') || b:autosave_bufwritepre != 0
    doau BufWritePre
  endif

  " Actually save the file! Will do nothing if the buffer has not file
  " allocated yet.
  silent! write

  " We don't always want to do the BufWritePost since it would clobber test
  " runners, linters or whatever. However, sometimes we actually do want it,
  " and for those times we specify this!
  if exists('b:autosave_bufwritepost') && b:autosave_bufwritepost == 1
    doau BufWritePost
  endif
endfunction

function! SetAutoSave()
  let b:autosave = finddir('.git', expand('%:p:h') . ';') != ""
endfunction

augroup autosave
  au!
  au InsertLeave,CursorHold,BufLeave * call AutoSave()
  au BufEnter,BufAdd * call SetAutoSave()
augroup END

" }}}
" Kill trailing whitespace {{{

function! KillTrailingWhitespace()
  " Set the position. Default is that the cursor will be placed on any match.
  let pos = getpos('.')

  " Remove trailing whitespace from any row. Ingore all errors.
  silent! %s/\s\+$//e

  " Remove trailing lines. Ignore all errors.
  silent! %s/\v\n+%$//e

  " Reset to the original position.
  call setpos('.',pos)
endfunction

augroup trailing
  au!
  au BufWritePre * call KillTrailingWhitespace()
augroup END

" }}}
" Wildmenu completion {{{

set wildmenu
set wildmode=longest:full

set wildignore+=.hg,.git,.svn                          " Version control
set wildignore+=*.jpg,*.bmp,*.gif,*.png,*.jpeg,*.xbm   " binary images
set wildignore+=*.o,*.obj,*.exe,*.dll,*.manifest       " compiled object files
set wildignore+=*.spl                                  " compiled spelling word lists
set wildignore+=*.sw?                                  " Vim swap files
set wildignore+=*.DS_Store                             " OSX bullshit
set wildignore+=*.mo                                   " Django i18n
set wildignore+=*.pyc,*__pycache__*                    " Python byte code
set wildignore+=*.egg-info*,*.egg                      " Python package data
set wildignore+=bin,build,lib,share,man                " Python venv files
set wildignore+=*__init__.py                           " Python inits
set wildignore+=*.db                                   " SQLite3
set wildignore+=*logs/*,*dist/*                         " ...

" }}}
" Line Return {{{

" Make sure Vim returns to the same line when you reopen a file.
" Thanks, Amit
augroup line_return
  au!
  au BufReadPost *
    \ if line("'\"") > 0 && line("'\"") <= line("$") |
    \   execute 'normal! g`"zvzz' |
    \ endif
augroup END

" }}}
" Tabs, spaces, wrapping {{{

set tabstop=2
set shiftwidth=2
set softtabstop=2
set expandtab
set nowrap
set textwidth=79
set formatoptions=qrn1tco
try
  set formatoptions+=j
catch /.*/
endtry
if exists('+colorcolumn')
  set colorcolumn=+1
endif

" }}}
" Backups {{{

set backup            " enable backups
set noswapfile          " It's 2014, Vim.

set viminfo='100,<50,s10,h,!,n~/.cache/vim/viminfo
if exists('+undodir')
  set undodir=~/.cache/vim/undo//   " undo files
endif
set backupdir=~/.cache/vim/backup// " backups
set directory=~/.cache/vim/swap//   " swap files

" }}}
" Searching {{{

set ignorecase
set smartcase
set incsearch
set showmatch
set hlsearch
set gdefault

set scrolloff=3
set sidescroll=1
set sidescrolloff=2

set virtualedit+=block

" }}}
" Leader {{{

" Vim, damnit, Why not <space>?
let mapleader = " "
let maplocalleader = "\\"

" }}}
" Color scheme {{{

" Jellybeans overrides
" Let's appreciate that this is so overriden it is almost it's own colorscheme.
let g:jellybeans_background_color = "151517"
let g:jellybeans_overrides = {
\'ColorColumn': {'guibg': '333333', 'ctermbg': 250, 'attr': 'bold'},
\'MatchParen':  {'guifg': 'db1515', 'guibg': '000000', 'ctermbg': "DarkCyan", 'attr': 'bold'},
\'StatusLine':  {'guifg': 'd62381', 'guibg': '202020', 'ctermbg': "White", 'attr': 'bold'},
\
\'Visual':      {'guibg': '333333', 'ctermbg': "233"},
\'VisualNOS':   {'guibg': '333333', 'ctermbg': "233"},
\
\'Cursor':      {'guifg': '000000', 'guibg': 'b0d0f0', 'ctermbg': "233"},
\'vCursor':     {'guifg': '000000', 'guibg': 'b0d0f0', 'ctermbg': "233"},
\'iCursor':     {'guifg': '000000', 'guibg': 'b0d0f0', 'ctermbg': "233", 'attr': 'bold'},
\
\'Constant':    {'guifg': '5faf5f', 'ctermfg': '71'},
\'Delimiter':   {'guifg': '668799', 'ctermfg': 'Grey'},
\'DiffAdd':     {'guifg': 'd2ebbe', 'guibg': '437019', 'ctermfg': 'White', 'ctermbg': 'DarkGreen'},
\'DiffChange':  {'guibg': '2b5b77', 'ctermfg': 'White', 'ctermbg': 'DarkBlue'},
\'DiffDelete':  {'guifg': '40000a', 'guibg': '700009', 'ctermfg': 'DarkRed', 'ctermbg': 'DarkRed'},
\'DiffText':    {'guifg': '8fbfdc', 'guibg': '000000', 'ctermfg': 'Yellow', 'attr': 'reverse'},
\'Function':    {'guifg': 'fad07a', 'ctermfg': 'Yellow'},
\'Identifier':  {'guifg': '97aad2', 'ctermfg': 'LightCyan', 'attr': ''},
\'Number':      {'guifg': 'ab6d5c', 'ctermfg': 'Red', 'attr': 'bold'},
\'Special':     {'guifg': 'bb565c', 'ctermfg': 'Red'},
\'Statement':   {'guifg': '8197bf', 'ctermfg': 'DarkBlue'},
\'Structure':   {'guifg': '8fbfdc', 'ctermfg': 'LightCyan'},
\"Folded":      {'guifg': "777777", 'guibg': "101010", 'ctermfg': "236", 'ctermbg': "235"},
\"Comment":     {'guifg': "555555", 'ctermfg': "Grey", 'attr': ''},
\}

syntax on
set background=dark
colorscheme jellybeans

function! BoldlyGo()
  let groups = [
    \ 'Function', 'PreProc', 'Statement', 'String', 'Number', 'Special',
    \ 'Comment', 'Constant'
    \ ]

  for group in groups
    exec "hi ".group." cterm=bold gui=bold"
  endfor
endfunction

" Run this everytime this file is sourced
call BoldlyGo()

" And also whenever you switch color schemes
augroup color_bolder
  au!

  autocmd ColorScheme * call BoldlyGo()
augroup end

" Highlight VCS conflict markers
augroup color_vcs
  au!
  au BufNewFile,BufRead * match Special /^\(<\|=\|>\)\{7\}\([^=].\+\)\?$/
augroup END

" }}}

" }}}
" Utility functions {{{

function! DisabledMapping(map) " {{{
  echohl Identifier
  echon 'Warning'
  echon ': '
  echohl Special
  echon a:map
  echohl None
  echon ' is a disabled mapping.'
  echohl None
endfunction " }}}
function! Clear() " {{{
  " noh can't be in here because that's how noh works
  call clearmatches()

  " Close all kinds of extra windows
  cclose
  lclose
  pclose

  " Close non-vim extra windows as well.
  let win = bufwinnr(bufname('%'))
  let bn = bufnr(bufname('%'))
  windo if index(['pytest', 'vimwiki'], &ft) != -1 | bd | endif
  if bufexists(bn)
    exe win . "wincmd w"
  endif

  " Clear the auto lists
  sign unplace *
  call setqflist([], {})
  if exists('*ToggleCoverage')
    call ToggleCoverage(1)
  endif

  " Also clear the message area
  echo
endfunction " }}}

" }}}
" Convenience mappings {{{

" Misc maps {{{

" I really don't understand why people love these so much.
nmap q <nop>

" Toggle line numbers
nnoremap <leader>n :setlocal number!<cr>

" Insert the directory of the current buffer in command line mode
cnoremap <expr> %% getcmdtype() == ':' ? expand('%:h').'/' : '%%'

" Readline bindings in command line mode
cnoremap <c-a> <home>
cnoremap <c-e> <end>

" Clear the screen
noremap <silent> <leader><space> :noh<cr>:call Clear()<cr>

" Formatting
nnoremap Q gqip
vnoremap Q gq

" Easier linewise reselection
nnoremap <leader>V V`]

" Normalization
nnoremap Y y$

" Reselect the visuals when indenting. Borks slightly with undoing, but
" you'll get used to it.
vnoremap < <gv
vnoremap > >gv

" Typos
command! -bang E e<bang>
command! -bang Q q<bang>
command! -bang W w<bang>
command! -bang QA qa<bang>
command! -bang Qa qa<bang>
command! -bang Wa wa<bang>
command! -bang WA wa<bang>
command! -bang Wq wq<bang>
command! -bang WQ wq<bang>

" Toggle paste
" For some reason pastetoggle doesn't redraw the screen (thus the status bar
" doesn't change) while :set paste! does, so I use that instead.
nnoremap <silent> <F6> :set paste!<cr>

" Unfuck my screen
nnoremap U :syntax sync fromstart<cr>:redraw!<cr>

" vim, I know.
nmap <silent> <c-c> :echo<cr>

" Let's stop using C-c to exit insert mode.
imap <c-c> <nop>
imap jk <esc>
imap jj <esc>

" }}}
" Insert Mode Completion {{{

inoremap <c-]> <c-x><c-]>

" }}}
" Quick split {{{

nmap <c-z> :sp<cr>
nmap <c-q> :q<cr>

" }}}
" Better for the hands {{{

nmap zh gT
nmap zl gt
exec "nmap zn :tabe "
exec "nmap zH :tab he "

" loclist mappings.
nmap ch :lpe<CR>
nmap cj :ll<CR>
nmap ck :lw<CR>
nmap cl :lne<CR>

" Same, but for quickfix
nmap cqh :cp<CR>
nmap cqj :cc<CR>
nmap cqk :cw<CR>
nmap cql :cn<CR>

" Diffing
nmap dn ]c
nmap dN [c

" }}}

" }}}
" Abbreviations {{{

" Symbols {{{

" Common programming symbol pairs that are just simpler to write
" alphanumerical. Write these and then space for abbreviation magic!
iabbrev eq ==
iabbrev ne !=
iabbrev lt <=
iabbrev gt >=
iabbrev pe +=
iabbrev hr =>

" }}}
" Spaceless {{{

function! EatChar(pat)
  let c = nr2char(getchar(0))
  return (c =~ a:pat) ? '' : c
endfunction

function! MakeSpacelessIabbrev(from, to)
  execute "iabbrev <silent> ".a:from." ".a:to."<C-R>=EatChar('\\s')<CR>"
endfunction

call MakeSpacelessIabbrev('rv', 'return_value')
call MakeSpacelessIabbrev('goc', 'get_or_create')
call MakeSpacelessIabbrev('cow', 'assert_called_once_with')
call MakeSpacelessIabbrev('cc', 'call_count')
call MakeSpacelessIabbrev('rq', 'request')
call MakeSpacelessIabbrev('rs', 'response')

" }}}

" }}}
" Searching and movement {{{

" Mappings {{{

" Don't move on initial *
nnoremap * *<c-o>

" Use enter to do browse to tags
nnoremap <cr> <c-]>zvzz

" Keep search matches in the middle of the window.
nnoremap n nzzzv
nnoremap N Nzzzv

" Same when jumping around
nnoremap <c-o> <c-o>zz

" }}}
" Directional Keys {{{

" It's 2014.
noremap j gj
noremap k gk
noremap gj j
noremap gk k

" Easy buffer navigation
noremap <C-h> <C-w>h
noremap <C-j> <C-w>j
noremap <C-k> <C-w>k
noremap <C-l> <C-w>l

noremap <leader>v <C-w>v

" }}}
" Visual Mode */# from Scrooloose {{{

function! s:VSetSearch()
  let temp = @@
  norm! gvy
  let @/ = '\V' . substitute(escape(@@, '\'), '\n', '\\n', 'g')
  let @@ = temp
endfunction

vnoremap * :<C-u>call <SID>VSetSearch()<CR>//<CR><c-o>
vnoremap # :<C-u>call <SID>VSetSearch()<CR>??<CR><c-o>

" }}}

" }}}
" Folding {{{

" Mappings {{{

" Start with an overview
set foldlevelstart=100

" Make zO recursively open whatever top level fold we're in, no matter where the
" cursor happens to be.
nnoremap zO zCzO

" why does this exist
nmap zE :call DisabledMapping('zE')<cr>

" Why have I been doing zR and zM for all these years?
nnoremap zr zRzz
nnoremap zm zMzz

nmap zR :call DisabledMapping('zR')<cr>
nmap zM :call DisabledMapping('zM')<cr>

nmap <leader>fm :set foldmethod=marker<cr>
nmap <leader>fi :set foldmethod=indent<cr>
nmap <leader>fx :set foldmethod=expr<cr>


" }}}
" Paragraph folding {{{

function! ParagraphFold(lnum)
  " See examples at :he fold-expr
  if getline(a:lnum) =~ '^\s*$' && getline(a:lnum+1) =~ '\S'
    return '<1'
  else
    return 1
  endif
endfunction

nmap <leader>fp :set foldmethod=expr foldexpr=ParagraphFold\(v:lnum\)<cr>

" }}}
" Foldtext {{{

function! FoldWithLines()
  " Folding function that puts the line counter indication at &tw if &tw is set
  " or at 79 if not.
  " Makes for readable fold information even if you only have one window open in
  " a wide instance of vim.

  let nucolwidth = &fdc + &number * &numberwidth
  let winwd = winwidth(0) - nucolwidth - 5
  let foldlinecount = foldclosedend(v:foldstart) - foldclosed(v:foldstart) + 1
  let foldinfo = " " . string(foldlinecount) . " lines "

  let linedata = ""

  " If there is a filetype specific foldtext method defined, use that to get the
  " foldtext data line.
  for ft in split(&ft, '\.')
    if exists('*FoldText_' . ft)
      let linedata = FoldText_{ft}(v:foldstart)
      break
    endif
  endfor

  if linedata == ""
    let linedata = getline(v:foldstart)
  endif

  let line = strpart(linedata, 0 , winwd - len(foldinfo)) . " "

  if &tw
    let base = &tw
  else
    let base = 79
  endif

  " Handle lines that are too long! Cut em down!
  if len(line) + len(foldinfo) > base
    let max = base - len(foldinfo) - 5
    let line = line[:max] . '... '
  endif

  let fillcharcount = base - len(line) - len(foldinfo)
  return line . repeat("-", fillcharcount) . foldinfo
endfunction

set foldtext=FoldWithLines()

" }}}

" }}}
" Filetype-specific {{{

" conf {{{

augroup ft_conf
  au!

  au BufNewFile,BufRead *.conf setlocal filetype=conf

  au Filetype conf setlocal foldmethod=marker
  au Filetype conf setlocal foldmarker={{{,}}}
augroup END

" }}}
" CSS and LessCSS {{{

augroup ft_css
  au!

  au BufNewFile,BufRead *.less setlocal filetype=less

  au Filetype less,css setlocal foldmethod=marker
  au Filetype less,css setlocal foldmarker={,}
  au Filetype less,css setlocal omnifunc=csscomplete#CompleteCSS
  au Filetype less,css setlocal iskeyword+=-
augroup END

" }}}
" git {{{

augroup ft_git
  au!
  au BufRead,BufNewFile *gitconfig set ft=gitconfig
augroup END

" }}}
" HTML {{{

augroup ft_html
  au!
  au FileType html,htmldjango setlocal tw=0
augroup END

" }}}
" Javascript, JSON {{{

augroup ft_javaish
  au!

  au FileType javascript,java setlocal foldmethod=marker foldmarker={,}
  au FileType javascript,java setlocal shiftwidth=2 softtabstop=2 expandtab textwidth=0
augroup END

augroup ft_json
  au!
  au BufEnter *.json setlocal shiftwidth=4 softtabstop=4 expandtab textwidth=0
  au BufEnter *.json setlocal foldmethod=marker foldmarker={,}
augroup END

" }}}
" Markdown {{{

let g:markdown_folding = 1

function! MarkdownPreview()
  exec 'pedit!' '.git/markdown/' . expand('%')
endfunction

augroup ft_markdown
  au!

  " au BufWritePost *.md,*.markdown silent Dispatch! auto-markdown %
  au BufNewFile,BufRead *.md,*.markdown setlocal filetype=markdown conceallevel=2
  au BufNewFile,BufRead *.md,*.markdown let b:autosave_bufwritepost = 1
  au Filetype markdown nnoremap <silent><buffer> <cr> :call MarkdownPreview()<cr>
augroup END

" }}}
" Mail {{{

augroup ft_mail
  au!

  au Filetype mail setl spell
augroup END

fun! AutoBlog(file)
  let file = substitute(a:file, '^\d\d\d\d-\d\d-\d\d-', '', '')
  let file = substitute(file, '\.markdown$', '', '')
  exec "silent !autoblog ".file." &> /dev/null &"
endfun

augroup ft_blog
  au!

  " au BufWritePost *_posts/* call AutoBlog(expand("<afile>:p:t"))
augroup END

" }}}
" Nginx {{{

augroup ft_nginx
  au!

  au BufRead,BufNewFile *nginx/*.conf set ft=nginx

  au FileType nginx set foldmethod=marker foldmarker={,} expandtab
  au FileType nginx set shiftwidth=4 softtabstop=4
  au FileType nginx set omnifunc=syntaxcomplete#Complete commentstring=#%s
augroup END

" }}}
" Python {{{

" Settings {{{

" Just add one shiftwidth when indenting python
let g:pyindent_open_paren = 4

augroup ft_python_settings
  au!

  " PEP8-style indenting.
  au Filetype python setl softtabstop=4 shiftwidth=4 expandtab

  " Snakecharmer folding
  " TODO: Actually set inside of snakecharmer
  au FileType python setl foldmethod=expr foldexpr=PythonFold\(v:lnum\)

  " Run syntastic checks automatically after autosave.
  au FileType python let b:autosave_bufwritepost = 0
augroup END

" }}}
" Snakecharmer {{{

augroup ft_python_snakecharmer
  au!

  " au CursorMovedI,CursorMoved *.py call SnakeArgs()
  " au CursorMovedI,CursorMoved *.py call SnakeMockArgs()
augroup END

" }}}
" Syntax {{{

" Give me ALL the syntax! |o/
let g:python_highlight_all = 1
let g:python_highlight_builtins = 1

augroup ft_python_syntax
  au!

  " Highlight self. How is this not the default?
  au BufEnter *.py syn keyword Special self

  " Special highlight for "return_value", the special part of mocks. This
  " separates it from the rest and makes it easier to see what is going on.
  au BufEnter *.py syn keyword Delimiter return_value

  " Dots and commas are delimiters - highlight them as such.
  au BufEnter *.py syn match Delimiter '[.,]'
augroup END

" }}}
" Switch {{{

function! PythonSelfToggle()
    let def = {
    \  'self\.\(\w\+\)\>': '\1',
    \ '\(self\.\)\@!\(\w\+\)\>': 'self.\2',
    \ }
  call switch#Switch([def])
endfunction

augroup ft_python_switch
  au!

  au FileType python nn <silent><buffer> S :call PythonSelfToggle()<cr>
  au FileType python let b:switch_custom_definitions =
    \ [
    \   {
    \     '\.debug(': '.info(',
    \     '\.info(': '.warning(',
    \     '\.warning(': '.error(',
    \     '\.error(': '.critical(',
    \     '\.critical(': '.exception(',
    \     '\.exception(': '.debug(',
    \   },
    \   {
    \     '\(not \)\@!in\>': 'not in',
    \     'not in\>': 'in',
    \   },
    \   {
    \     '\<\(if\|and\|or\|assert\|is\)\( not\)\@!': '\1 not',
    \     '\<\(if\|and\|or\|assert\|is\) not': '\1',
    \   },
    \   ['==', '!='],
    \   [' % ', '.format'],
    \   ['True', 'False'],
    \   ['from', 'import'],
    \   ['return', 'yield'],
    \   ['def', 'class'],
    \   ['getattr', 'hasattr'],
    \   ['setUp', 'tearDown'],
    \   ['setup_', 'teardown_'],
    \   ['@patch', '@patch.object'],
    \   ['request', 'response'],
    \   {
    \     '\(^\s*except \S\+\):': '\1 as exc:',
    \     '\(^\s*except \S\+\) as \(\S*\):': '\1:',
    \   },
    \   {
    \     '\(^\s*class \S\+\)():': '\1(object):',
    \     '\(^\s*class \S\+\)(object):': '\1():',
    \   },
    \   {
    \     '\(^\s\+def \S\+\)(self\(, \)\?\([^)]*\)\{-}):': '\1(\3):',
    \     '\(^\s\+def \S\+\)():': '\1(self):',
    \     '\(^\s\+def \S\+\)(\(self, \)\@!\([^)]*\)):': '\1(self, \3):',
    \   },
    \   {
    \     "\\(^\\s*\\)[\"']\\(\\S\\+\\)[\"']:\\s*\\(.*\\),\?$": '\1\2=\3',
    \     '\(^\s*\)\(\S\+\)\s*=\s*\(.*\)$': "\\1'\\2': \\3,",
    \   },
    \ ]
augroup END

" }}}

" }}}
" Puppet {{{

augroup ft_puppet
  au!
  au BufEnter *.pp set commentstring=#%s
augroup END

" }}}
" tmux {{{

augroup ft_tmux
  au!
  au BufEnter *tmux/*.conf setf tmux
  au FileType tmux set commentstring=#%s
augroup END

" }}}
" Vim {{{

function! HelpExpand()
  try
    let word = expand('<cword>')
    exec "help" word
  catch /.*/
    echo "No help for" word
  endtry
endfunction

augroup ft_help
  au!

  au FileType help setlocal textwidth=78
  au FileType help nnoremap <buffer> <cr> <C-]>
augroup END

augroup ft_quickfix
  au!

  " Disable any mapping of enter in quickfix buffers
  au Filetype qf nnoremap <cr> <cr>
  au Filetype qf setlocal colorcolumn=0 nolist nowrap tw=0
augroup END

augroup ft_vim
  au!

  " If on real vimrc, source it upon save
  au BufWritePost *vimrc* let fn = expand("<afile>:p") | if fn !~ '://'
        \ | source `=fn` | call lightline#disable() | call lightline#enable() | endif

  " If in any other vimscript, source it immediately via scriptease
  au BufWritePost *.vim let fn = expand("<afile>:p") | if fn !~ '://'
        \ | silent Runtime | endif

  au FileType vim setlocal foldmethod=marker
  au FileType vim setlocal omnifunc=syntaxcomplete#Complete
  au FileType vim nnoremap <buffer> <silent> <bs> :call HelpExpand()<cr>
augroup END

" }}}
" Xresources {{{

augroup ft_xresources
  au!

  au BufNewFile,BufRead *Xdefaults,*Xresources setlocal filetype=xdefaults
  au BufWritePost *Xdefaults,*Xresources exe "silent !xrdb -merge " expand("<afile>:p")
  au Filetype xdefaults set commentstring=!%s
augroup END

" }}}
" zsh {{{

" Automatically add zsh ft and shebang when making tool scripts
function! AutoUtil(fn)
  if a:fn =~ '\.'
    " Abort if there seems there is an extension
    return
  endif

  setl ft=zsh
  call append(0, ['#!/bin/zsh'])
endfunction

augroup ft_zshutil
  au!

  au BufNewFile */util/*,*/tools/* call AutoUtil(expand("<afile>:p:t"))
augroup END

" }}}

" }}}
" Plugin settings {{{

" Airline {{{

let g:airline_powerline_fonts=0
let g:airline_left_sep = ''
let g:airline_right_sep = ''
let g:airline_fugitive_prefix = ''

" }}}
" Commentary {{{

nmap \ gcc
vmap \ gc
vmap <bar> gc
nmap <bar> gcap

" Unmap the \\ bindings so that \ is the terminating map
try
  unmap \\u
  unmap \\\
  unmap \\
catch /.*/
endtry


" }}}
" Ctrl-P {{{

let g:ctrlp_dont_split = 'NERD_tree_2'
let g:ctrlp_jump_to_buffer = 0
let g:ctrlp_map = '<F3>'
let g:ctrlp_working_path_mode = 0
let g:ctrlp_match_window_reversed = 1
let g:ctrlp_split_window = 0
let g:ctrlp_max_height = 20
let g:ctrlp_extensions = ['tag', 'project']
let g:ctrlp_custom_ignore = '\v[\/]\.\?(git|hg|svn|lib|build|dist)'

let g:ctrlp_prompt_mappings = {
\ 'PrtSelectMove("j")':   ['<c-j>', '<down>', '<s-tab>'],
\ 'PrtSelectMove("k")':   ['<c-k>', '<up>', '<tab>'],
\ 'PrtHistory(-1)':     ['<c-n>'],
\ 'PrtHistory(1)':    ['<c-p>'],
\ 'ToggleFocus()':    ['<c-tab>'],
\ }

let g:ctrlp_projects = [
      \ '~/spotify', '~/git', '~/.vim/bundle', '~/var/http/talks',
      \ '~/var/go/src/github.com/thiderman']

function! MaybeCtrlP()
  if getcwd() == expand('$HOME')
    CtrlPProject
  else
    CtrlP
  endif
endfunction

nnoremap <silent> <A-q> :call MaybeCtrlP()<cr>
nnoremap <silent> <A-a> :CtrlPBuffer<cr>
nmap     <silent> <A-e> :CtrlP %%<cr>
nnoremap <silent> <A-p> :CtrlPProject<cr>

nnoremap <silent> <leader>t :CtrlPTag<cr>
nnoremap <silent> <Leader>q :call MaybeCtrlP()<cr>
nnoremap <silent> <Leader>a :CtrlPBuffer<cr>
nnoremap <silent> <Leader>p :CtrlPProject<cr>
nmap     <silent> <leader>e :CtrlP %%<cr>
nnoremap <silent> <Leader>Q :CtrlPReload<cr>

" }}}
" Endwise {{{

" For whatever reason, delimitmate and endwise are clashing for me. Just set
" the enter binding here instead.
imap <C-X><CR> <Plug>delimitMateCR<Plug>AlwaysEnd
imap <CR>      <Plug>delimitMateCR<Plug>DiscretionaryEnd

" }}}
" Delimitmate {{{

let delimitMate_expand_space = 1

" }}}
" Fugitive {{{

fun! s:gm(short, cmd, ...)  " git_map
  let silent = a:cmd =~ '\w$' ? '<silent>' : ''
  let cr = a:cmd =~ '\w$' ? '<cr>' : ''
  let extra = a:0 ? a:1 : ''
  exec "nmap" silent a:short ":" . a:cmd . cr . extra
endfunction

call s:gm('gi',  'Git ')
call s:gm('ge',  'Gedit')
call s:gm('gb',  'Gblame')

call s:gm('gle', 'Extradite')
call s:gm('gll', 'Gitv')
call s:gm('glo', 'Glog ')
call s:gm('gpp', 'Git push origin')
call s:gm('gpf', 'Git push origin -f')
call s:gm('guu', 'Git pull --no-edit origin')
call s:gm('gre', 'Gread ')
call s:gm('grr', 'Gremove ')

call s:gm('gs',  'Gstatus', '<c-n>')
call s:gm('gw',  'Gwrite')
call s:gm('gm',  'Gmove ')

nmap <silent> gd <c-w>o:Gdiff<cr>

" }}}
" Gundo {{{

nmap <leader>g :GundoToggle<cr>

" }}}
" Lightline {{{

let g:lightline = {
      \ 'colorscheme': 'jellybeans',
      \ 'active': {
      \   'left': [
      \      ['mode', 'paste'],
      \      ['fugitive', 'filename', 'pytest'],
      \      ['syntastic', 'ctrlpmark']
      \   ],
      \   'right': [
      \     ['lineinfo'],
      \     ['percent'],
      \     ['filetype']
      \   ]
      \ },
      \ 'component_function': {
      \   'fugitive': 'LLFugitive',
      \   'filename': 'LLFilename',
      \   'fileformat': 'LLFileformat',
      \   'filetype': 'LLFiletype',
      \   'fileencoding': 'LLFileencoding',
      \   'mode': 'LLMode',
      \   'ctrlpmark': 'CtrlPMark',
      \ },
      \ 'component_expand': {
      \   'syntastic': 'SyntasticStatuslineFlag',
      \   'pytest': 'PytestCurrent',
      \ },
      \ 'component_type': {
      \   'syntastic': 'error',
      \   'pytest': 'error',
      \ },
      \ 'subseparator': {'left': '│', 'right': '│'}
      \ }

function! LLModified()
  return &ft =~ 'help' ? '' : &modified ? '+' : &modifiable ? '' : '-'
endfunction

function! LLReadonly()
  return &ft !~? 'help' && &readonly ? 'RO' : ''
endfunction

function! LLFilename()
  let fname = expand('%')
  return fname == 'ControlP' ? g:lightline.ctrlp_item :
        \ &ft == 'qf' ? get(w:, 'quickfix_title', 'Quickfix') :
        \ fname =~ '__Gundo\|NERD_tree' ? '' :
        \ ('' != LLReadonly() ? LLReadonly() . ' ' : '') .
        \ ('' != fname ? fname : '[No Name]') .
        \ ('' != LLModified() ? ' ' . LLModified() : '')
endfunction

function! LLFugitive()
  try
    if exists('*fugitive#head')
      let mark = ''  " edit here for cool mark
      let _ = fugitive#head()
      return strlen(_) ? mark._ : ''
    endif
  catch
  endtry
  return ''
endfunction

function! LLFileformat()
  return winwidth(0) > 70 ? &fileformat : ''
endfunction

function! LLFiletype()
  return winwidth(0) > 70 ? (strlen(&filetype) ? &filetype : 'no ft') : ''
endfunction

function! LLFileencoding()
  return winwidth(0) > 70 ? (strlen(&fenc) ? &fenc : &enc) : ''
endfunction

function! LLMode()
  let fname = expand('%:t')
  return fname == '__Tagbar__' ? 'Tagbar' :
        \ fname == 'ControlP' ? 'CtrlP' :
        \ fname == '__Gundo__' ? 'Gundo' :
        \ fname == '__Gundo_Preview__' ? 'Gundo Preview' :
        \ fname =~ 'NERD_tree' ? 'NERDTree' :
        \ winwidth(0) > 60 ? lightline#mode() : ''
endfunction

function! CtrlPMark()
  if expand('%:t') =~ 'ControlP'
    call lightline#link('iR'[g:lightline.ctrlp_regex])
    return lightline#concatenate([g:lightline.ctrlp_prev, g:lightline.ctrlp_item
          \ , g:lightline.ctrlp_next], 0)
  else
    return ''
  endif
endfunction

let g:ctrlp_status_func = {
  \ 'main': 'CtrlPStatusFunc_1',
  \ 'prog': 'CtrlPStatusFunc_2',
  \ }

function! CtrlPStatusFunc_1(focus, byfname, regex, prev, item, next, marked)
  let g:lightline.ctrlp_regex = a:regex
  let g:lightline.ctrlp_prev = a:prev
  let g:lightline.ctrlp_item = a:item
  let g:lightline.ctrlp_next = a:next
  return lightline#statusline(0)
endfunction

function! CtrlPStatusFunc_2(str)
  return lightline#statusline(0)
endfunction

let g:tagbar_status_func = 'TagbarStatusFunc'

function! TagbarStatusFunc(current, sort, fname, ...) abort
    let g:lightline.fname = a:fname
  return lightline#statusline(0)
endfunction

augroup AutoSyntastic
  autocmd!
  autocmd BufWritePost *.c,*.cpp,*.py call s:syntastic()
augroup END

function! s:syntastic()
  SyntasticCheck
  call lightline#update()
endfunction

" }}}
" NERDTree {{{

noremap  <F2> :NERDTreeToggle<cr>
inoremap <F2> <esc>:NERDTreeToggle<cr>

augroup ps_nerdtree
  au!

  au Filetype nerdtree setlocal nolist
  " au Filetype nerdtree nnoremap <buffer> K :q<cr>
augroup END

let NERDTreeHighlightCursorline = 1
let NERDTreeIgnore = ['\~$', '.*\.pyc$', 'pip-log\.txt$', 'whoosh_index',
          \ 'xapian_index', '.*.pid', 'monitor.py', '.*-fixtures-.*.json',
          \ '.*\.o$', 'db.db', 'tags.bak']

let NERDTreeMinimalUI = 1
let NERDTreeDirArrows = 1
let NERDChristmasTree = 1
let NERDTreeChDirMode = 2
let NERDTreeMapJumpFirstChild = 'gK'

" }}}
" Projectile {{{

nmap <silent> <c-w>a :AV<cr>
nmap <silent> <c-w>A :AS<cr>

" }}}
" Syntastic {{{

let g:syntastic_always_populate_loc_list = 1
let g:syntastic_enable_signs = 1
let g:syntastic_check_on_open = 0
let g:syntastic_disabled_filetypes = ['html', 'rst', 'css', 'zsh']
let g:syntastic_stl_format = '[%E{%e Errors}%B{, }%W{%w Warnings}]'
let g:syntastic_javascript_checker = "jshint"

" }}}
" Switch {{{

nnoremap <silent> - :Switch<cr>
nnoremap <silent> <a-w> :Switch<cr>
nnoremap <silent> <leader>c :Switch<cr>

" }}}
" surround {{{

" While I love surround.vim, the s/S confusion in visual mode does not
" really tickle my fancy. To make up for this, these mappings happened.

vmap ' S'
vmap ( S)
vmap [ S]
vmap { S}

" }}}
" UltiSnips {{{

let g:UltiSnipsExpandTrigger = '<C-f>'
let g:UltiSnipsJumpForwardTrigger="<c-j>"
let g:UltiSnipsJumpBackwardTrigger="<c-k>"
inoremap <c-x><c-k> <c-x><c-k>

" }}}
" unimpaired {{{

" gvim only alt-bindings
nmap <A-j> ]e
nmap <A-k> [e
vmap <A-j> ]egv
vmap <A-k> [egv

" Terminal binds. A bit clunky, but hey
" nmap zj ]e
" nmap zk [e
" vmap zj ]egv
" vmap zk [egv
vmap <C-j> ]egv
vmap <C-k> [egv

" }}}
" vimwiki {{{

let g:vimwiki_list = [
  \ {
  \    'path': '~/.local/share/vimwiki/main/',
  \    'path_html': '~/.local/share/vimwiki/html'
  \ }
  \ ]
let g:vimwiki_folding = 'syntax'

function! ProjectTodo(...)
  " Save, close and clear message buffer if we are already in a vimwiki buffer
  if &ft == 'vimwiki'
    update
    bdelete
    echo
    return
  endif

  let project = a:0 ? a:1 : fnamemodify(getcwd(), ':t')
  let dir = expand(g:vimwiki_list[0]['path'])
  let file = dir . project . '.wiki'
  split `=file`
endfunction

nmap <silent> <A-t> :call ProjectTodo()<cr>
" nmap <silent> <leader>t :call ProjectTodo()<cr>
command! -nargs=? ProjectTodo :call ProjectTodo(<q-args>)

function! Diary()
  let root = expand(g:vimwiki_list[0]['path']) . 'diary/'
  let file = root . strftime('%Y-%m-%d.wiki')

  " If no previous entry exists, grab the list from the last one.
  if !filereadable(file)
    call writefile(readfile(split(globpath(root, '2*'), '\n')[-1]), file)
  endif

  edit `=file`
endfunction

nmap <silent> <leader>wg :call Diary()<cr>
command! -nargs=0 Diary :call Diary()

" }}}
" yankstack {{{

nmap <leader>z <Plug>yankstack_substitute_older_paste
nmap <leader>Z <Plug>yankstack_substitute_newer_paste

" }}}

" }}}
" Mini-plugins {{{

" Ack motions {{{

" Motions to Ack for things.  Works with pretty much everything, including:
"
"   w, W, e, E, b, B, t*, f*, i*, a*, and custom text objects
"
" Awesome.
"
" Note: If the text covered by a motion contains a newline it won't work.  Ack
" searches line-by-line.

" Only do this if we actually have the silver searcher
if executable('ag')
  let g:ackprg = 'ag --smart-case --nogroup --nocolor --column'

  nnoremap <silent> <leader>A :set opfunc=<SID>AckMotion<CR>g@
  xnoremap <silent> <leader>A :<C-U>call <SID>AckMotion(visualmode())<CR>

  nnoremap <bs> :Ack! '\b<c-r><c-w>\b'<cr>
  xnoremap <silent> <bs> :<C-U>call <SID>AckMotion(visualmode())<CR>

  function! s:CopyMotionForType(type)
    if a:type ==# 'v'
      silent execute "normal! `<" . a:type . "`>y"
    elseif a:type ==# 'char'
      silent execute "normal! `[v`]y"
    endif
  endfunction

  function! s:AckMotion(type) abort
    let reg_save = @@

    call s:CopyMotionForType(a:type)

    execute "normal! :Ack! --literal " . shellescape(@@) . "\<cr>"

    let @@ = reg_save
  endfunction
endif

" }}}
" Highlight Word {{{
"
" This mini-plugin provides a few mappings for highlighting words temporarily.
"
" Sometimes you're looking at a hairy piece of code and would like a certain
" word or two to stand out temporarily.  You can search for it, but that only
" gives you one color of highlighting.  Now you can use <leader>N where N is
" a number from 1-6 to highlight the current word in a specific color.

function! HiInterestingWord(n, ...) " {{{
  " Save our location.
  normal! mz

  if a:0
    " If argument was given, use that word.
    let pat = '\V' . escape(a:1, '\')
  else
    " Yank the current word into the z register.
    normal! "zyiw
    " Construct a literal pattern that has to match at boundaries.
    let pat = '\V\<' . escape(@z, '\') . '\>'
  endif

  " Calculate an arbitrary match ID.  Hopefully nothing else is using it.
  let mid = 86750 + a:n

  " Clear existing matches, but don't worry if they don't exist.
  silent! call matchdelete(mid)

  " Actually match the words.
  call matchadd("InterestingWord" . a:n, pat, 1, mid)

  " Move back to our original location.
  normal! `z
endfunction " }}}

" Mappings {{{

nnoremap <silent> <leader>1 :call HiInterestingWord(1)<cr>
nnoremap <silent> <leader>2 :call HiInterestingWord(2)<cr>
nnoremap <silent> <leader>3 :call HiInterestingWord(3)<cr>
nnoremap <silent> <leader>4 :call HiInterestingWord(4)<cr>
nnoremap <silent> <leader>5 :call HiInterestingWord(5)<cr>
nnoremap <silent> <leader>6 :call HiInterestingWord(6)<cr>

" }}}
" Default Highlights {{{

hi def InterestingWord1 guifg=#000000 ctermfg=16 guibg=#ffa724 ctermbg=214
hi def InterestingWord2 guifg=#000000 ctermfg=16 guibg=#aeee00 ctermbg=154
hi def InterestingWord3 guifg=#000000 ctermfg=16 guibg=#8cffba ctermbg=121
hi def InterestingWord4 guifg=#000000 ctermfg=16 guibg=#b88853 ctermbg=137
hi def InterestingWord5 guifg=#000000 ctermfg=16 guibg=#ff9eb8 ctermbg=211
hi def InterestingWord6 guifg=#000000 ctermfg=16 guibg=#ff2c4b ctermbg=195

" }}}

" }}}
" Font resizer {{{

function! GuiFont(size)
  let spl = split(a:size == 0 ? g:guifont_save : &guifont, ' ')
  let size = spl[-1]
  exec "set guifont=" . join(spl[:-2], '\ ') . '\ ' . (size + a:size)

  wincmd =
  redraw!

  echo "Fontsize: " . (size + a:size)
endfunction

let g:guifont_save = &guifont
nmap <silent> <c-up> :<C-U>call GuiFont(1)<cr>
nmap <silent> <c-down> :<C-U>call GuiFont(-1)<cr>
nmap <silent> <c-left> :<C-U>call GuiFont(0)<cr>

" }}}
" Jekyll post {{{

let g:jekyll_root = expand("~/git/thiderman/_posts")

function! JekyllPost(slug)
  let fn = g:jekyll_root.'/'.strftime('%Y-%m-%d').'-'.a:slug.'.markdown'
  edit `=fn`
endfunction

command! -nargs=1 Blog :call JekyllPost(<q-args>)

" }}}
" Directory creator {{{

augroup dir_create
  au!
  au BufWritePre,FileWritePre * call Cdir()
augroup END

function! Cdir()
  let dir = expand('<afile>:p:h')
  if !isdirectory(dir)
    call mkdir(dir, 'p')
  endif
endfunction

" }}}

" }}}
" Environments (GUI/Console) {{{

if has('gui_running')
  " Only do these on startup. It's annoying when you've changed it and it
  " resets just because you save this very file.
  if has('vim_starting')
    " Remove all the UI cruft
    set guioptions=cagit  " Disable graphical elements
    set guifont=Inconsolata\ 10
  endif

  " Different cursors for different modes.
  set guicursor=n-c:block-Cursor-blinkon0
  set guicursor+=v:block-vCursor-blinkon0
  set guicursor+=i-ci:ver20-iCursor
endif

" }}}
" User specific settings {{{

let userfile = globpath(&runtimepath, 'local.vim')
if filereadable(userfile)
  execute 'source ' . userfile
endif

" }}}
